import com.dirzaaulia.gamewish.buildsrc.Libs
import com.dirzaaulia.gamewish.buildsrc.Versions

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin
        classpath Libs.Hilt.androidGradlePlugin
        classpath Libs.Gms.plugin
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.14.2'
}

subprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
        maven {
            url "https://jitpack.io"
            credentials { username "jp_uvj8t2utirothfevp7gb6l9udn" }
        }

        if (!Libs.AndroidX.Compose.snapshot.isEmpty()) {
            maven { url "https://androidx.dev/snapshots/builds/${Libs.AndroidX.Compose.snapshot}/artifacts/repository/" }
        }
        if (Libs.Accompanist.version.endsWith("SNAPSHOT")) {
            maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        }

    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            // Treat all Kotlin warnings as errors (disabled by default)
            allWarningsAsErrors = project.hasProperty("warningsAsErrors") ? project.warningsAsErrors : false
            // Opt-in to experimental compose APIs
            freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'

            jvmTarget = '1.8'
        }
    }

    configurations.configureEach {
        // androidx.test forces JUnit, 4.12, prefer 4.13
        resolutionStrategy {
            force Libs.JUnit.junit
        }
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')
            ktlint(Versions.ktlint).userData([android: "true"])
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }
    }
}